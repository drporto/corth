MACRO PUTS 1 3  1 syscall END
MACRO EXIT   1 60 syscall END

MACRO SIZE 127 END
MACRO RULE 110 END
MACRO PATTERN_MEMORY_POS mem 1000 + END

PATTERN_MEMORY_POS 0 .

SIZE 2 * 1 + 0
while 2dup > do
    dup mem + 0 .
    1 +
end
drop drop

mem SIZE + 2 - 1 .
mem SIZE 2 * + '\n' .

SIZE 0
while 2dup > do

    SIZE 0
    while 2dup > do
        if dup mem + , then
            dup mem + SIZE + '@' .
        else
            dup mem + SIZE + ' ' .
        end
        1 +
    end
    drop drop

    SIZE 1 + mem SIZE + PUTS

    mem     , 2 <<
    mem 1 + , 1 << |
    PATTERN_MEMORY_POS swap .
    SIZE 0
    while 2dup swap 2 - swap > do
        dup 2 mem + + , PATTERN_MEMORY_POS , 1 << 7 & | PATTERN_MEMORY_POS swap .
        dup 1 mem + + RULE PATTERN_MEMORY_POS , >> 1 & .
        1 +
    end
    drop drop

    mem 0 .
    mem SIZE + 1 - 0 .

    1 +
end
drop drop
0 EXIT